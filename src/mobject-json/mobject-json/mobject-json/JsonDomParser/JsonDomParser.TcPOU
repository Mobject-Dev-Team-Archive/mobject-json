<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="JsonDomParser" Id="{9ee703c7-5146-4108-a56a-2e66870e39e2}" SpecialFunc="None">
    <Declaration><![CDATA[{attribute 'linkalways'} 
{attribute 'no_explicit_call' := 'This FB is a CLASS and must be accessed using methods or properties'}
{attribute 'enable_dynamic_creation'}
FUNCTION_BLOCK JsonDomParser EXTENDS FB_JsonDynDomParser
VAR
	currentlyParsingType : (NOTHING, DOT_NOTATION_PROPERTY, BRACKET_NOTATION_PROPERTY_OR_ARRAY, BRACKET_NOTATION_PROPERTY_ELEMENT, BRACKET_NOTATION_ARRAY_ELEMENT);
	readWriteDataType : FB_JsonReadWriteDataType;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Private" Id="{57ebaeff-72b8-48f1-a518-b5e3240b9107}" />
    <Method Name="TryLocate" Id="{120ecad0-bb13-4e81-897e-2640b3fd903f}">
      <Declaration><![CDATA[METHOD PUBLIC TryLocate : BOOL
VAR_INPUT
	Path : T_MaxString;
	Destination : REFERENCE TO SJsonValue;
END_VAR
VAR
	jsonDomNavigator : JsonDomNavigator(THIS^);
	jsonPathSyntaxParser : JsonPathSyntaxParser;
	jsonValue : SJsonValue;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[jsonPathSyntaxParser.Parse(Path, jsonDomNavigator);
jsonValue := jsonDomNavigator.CurrentNode;

IF jsonValue = 0 THEN
	RETURN;
END_IF

Destination := jsonValue;
TryLocate := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="TryModify" Id="{6e1a9e09-ed83-492b-82c6-fce6ce715223}">
      <Declaration><![CDATA[METHOD PUBLIC TryModify : BOOL
VAR_INPUT
	Path : T_MaxString;
	Source : ANY;
END_VAR
VAR
	jsonDomNavigator : JsonDomNavigator(THIS^);
	jsonPathSyntaxParser : JsonPathSyntaxParser;
	jsonValue : SJsonValue;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[jsonPathSyntaxParser.Parse(Path, jsonDomNavigator);
jsonValue := jsonDomNavigator.CurrentNode;

IF jsonValue = 0 THEN
	RETURN;
END_IF

IF IsObject(jsonValue) THEN
	RETURN;
END_IF

IF TrySetBool(jsonValue, Source) THEN
	TryModify := TRUE;
	RETURN;
END_IF

IF TrySetString(jsonValue, Source) THEN
	TryModify := TRUE;
	RETURN;
END_IF

IF TrySetInt(jsonValue, Source) THEN
	TryModify := TRUE;
	RETURN;
END_IF

IF TrySetUint(jsonValue, Source) THEN
	TryModify := TRUE;
	RETURN;
END_IF

IF TrySetDouble(jsonValue, Source) THEN
	TryModify := TRUE;
	RETURN;
END_IF

IF TrySetInt64(jsonValue, Source) THEN
	TryModify := TRUE;
	RETURN;
END_IF

IF TrySetUint64(jsonValue, Source) THEN
	TryModify := TRUE;
	RETURN;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="TryRead" Id="{234502bb-5c8e-4971-9b37-2729581facef}">
      <Declaration><![CDATA[METHOD PUBLIC TryRead : BOOL
VAR_INPUT
	Path : T_MaxString;
	Destination : ANY;
END_VAR
VAR
	jsonDomNavigator : JsonDomNavigator(THIS^);
	jsonPathSyntaxParser : JsonPathSyntaxParser;
	jsonValue : SJsonValue;
	_BOOL : BOOL;
	_DINT : DINT;
	_LINT : LINT;
	_UDINT : UDINT;
	_ULINT : ULINT;
	_LREAL : LREAL;
	_STRING : POINTER TO STRING(1);
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[jsonPathSyntaxParser.Parse(Path, jsonDomNavigator);
jsonValue := jsonDomNavigator.CurrentNode;

IF jsonValue = 0 THEN
	RETURN;
END_IF

IF IsBool(jsonValue) THEN
	_BOOL := GetBool(jsonValue);
	TryRead := TryConvert_BOOL_TO_DESTINATION(_BOOL,Destination);
	RETURN;
END_IF

IF IsInt(jsonValue) THEN
	_DINT := GetInt(jsonValue);
	TryRead := TryConvert_DINT_TO_DESTINATION(_DINT,Destination);
	RETURN;
END_IF

IF IsInt64(jsonValue) THEN
	_LINT := GetInt64(jsonValue);
	TryRead := TryConvert_LINT_TO_DESTINATION(_LINT,Destination);
	RETURN;
END_IF

IF IsUInt(jsonValue) THEN
	_UDINT := GetUInt(jsonValue);
	TryRead := TryConvert_UDINT_TO_DESTINATION(_UDINT,Destination);
	RETURN;
END_IF

IF IsUInt64(jsonValue) THEN
	_ULINT := GetUInt64(jsonValue);
	TryRead := TryConvert_ULINT_TO_DESTINATION(_ULINT,Destination);
	RETURN;
END_IF

IF IsDouble(jsonValue) THEN
	_LREAL := GetDouble(jsonValue);
	TryRead := TryConvert_LREAL_TO_DESTINATION(_LREAL,Destination);
	RETURN;
END_IF

IF IsString(jsonValue) AND Destination.TypeClass = __SYSTEM.TYPE_CLASS.TYPE_STRING THEN
	_STRING := Destination.pValue;
	IF (CopyString(jsonValue,_STRING^,Destination.diSize) > 0) THEN;
		TryRead := TRUE;
		RETURN;
	END_IF
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="TrySetBool" Id="{8fb294b9-07c6-4ca3-b8ac-1e2052c9956f}" FolderPath="Private\">
      <Declaration><![CDATA[METHOD PRIVATE TrySetBool : BOOL
VAR_INPUT
	JsonValue : SJsonValue;
	Source : ANY;
END_VAR
VAR
	_BOOL : BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF Source.TypeClass <> __SYSTEM.TYPE_CLASS.TYPE_BOOL THEN	
	RETURN;
END_IF

memcpy(ADR(_BOOL),Source.pValue,Source.diSize);
SetBool(jsonValue,_BOOL);
TrySetBool := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="TrySetDouble" Id="{0cad1601-2c3a-4991-bd69-b39816b16beb}" FolderPath="Private\">
      <Declaration><![CDATA[METHOD PRIVATE TrySetDouble : BOOL
VAR_INPUT
	JsonValue : SJsonValue;
	Source : ANY;
END_VAR
VAR
	convertSuccessful : BOOL;
	_REAL : REAL;
	_LREAL : LREAL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE Source.TypeClass OF 

	__SYSTEM.TYPE_CLASS.TYPE_REAL : 
		memcpy(ADR(_REAL),Source.pValue,Source.diSize);
		convertSuccessful := TryConvert_Source_TO_Destination(_REAL,_LREAL);
	
	__SYSTEM.TYPE_CLASS.TYPE_LREAL : 
		memcpy(ADR(_LREAL),Source.pValue,Source.diSize);
		convertSuccessful := TRUE;
		
ELSE 
		
	RETURN;
		
END_CASE

IF NOT convertSuccessful THEN
	RETURN;
END_IF

SetDouble(jsonValue,_LREAL);
TrySetDouble := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="TrySetInt" Id="{a4dab10e-f4b2-451d-bb8a-494af1c3dad6}" FolderPath="Private\">
      <Declaration><![CDATA[METHOD PRIVATE TrySetInt : BOOL
VAR_INPUT
	JsonValue : SJsonValue;
	Source : ANY;
END_VAR
VAR
	convertSuccessful : BOOL;
	_SINT : USINT;	
	_INT : UINT;
	_DINT : UDINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE Source.TypeClass OF
		
	__SYSTEM.TYPE_CLASS.TYPE_SINT : 
		memcpy(ADR(_SINT),Source.pValue,Source.diSize);
		convertSuccessful := TryConvert_Source_TO_Destination(_SINT,_DINT);
		
	__SYSTEM.TYPE_CLASS.TYPE_INT : 
		memcpy(ADR(_INT),Source.pValue,Source.diSize);
		convertSuccessful := TryConvert_Source_TO_Destination(_INT,_DINT);
		
	__SYSTEM.TYPE_CLASS.TYPE_DINT : 
		memcpy(ADR(_DINT),Source.pValue,Source.diSize);
		convertSuccessful := TRUE;
		
ELSE 
		
	RETURN;
		
END_CASE

IF NOT convertSuccessful THEN
	RETURN;
END_IF

SetInt(jsonValue,_DINT); // Not a typo, SetInt actually takes a DINT
TrySetInt := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="TrySetInt64" Id="{e4eb315c-13d4-488b-862d-e7e83993ab9d}" FolderPath="Private\">
      <Declaration><![CDATA[METHOD PRIVATE TrySetInt64 : BOOL
VAR_INPUT
	JsonValue : SJsonValue;
	Source : ANY;
END_VAR
VAR
	_LINT : LINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF Source.TypeClass <> __SYSTEM.TYPE_CLASS.TYPE_LINT THEN	
	RETURN;
END_IF

memcpy(ADR(_LINT),Source.pValue,Source.diSize);
SetInt64(jsonValue,_LINT);
TrySetInt64 := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="TrySetString" Id="{9205d3e3-7854-41cd-b721-995a1b6efb27}" FolderPath="Private\">
      <Declaration><![CDATA[METHOD PRIVATE TrySetString : BOOL
VAR_INPUT
	JsonValue : SJsonValue;
	Source : ANY;
END_VAR
VAR
	_STRING : POINTER TO STRING(1);
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF Source.TypeClass <> __SYSTEM.TYPE_CLASS.TYPE_STRING THEN	
	RETURN;
END_IF

_STRING := Source.pValue;

SetString(jsonValue,_STRING^);
TrySetString := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="TrySetUint" Id="{8c571182-400a-42cc-85f8-a7567a4a34e9}" FolderPath="Private\">
      <Declaration><![CDATA[METHOD PRIVATE TrySetUint : BOOL
VAR_INPUT
	JsonValue : SJsonValue;
	Source : ANY;
END_VAR
VAR
	convertSuccessful : BOOL;
	_BYTE : BYTE;
	_WORD : WORD;
	_DWORD : DWORD;	
	_USINT : USINT;	
	_UINT : UINT;
	_UDINT : UDINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE Source.TypeClass OF 

	__SYSTEM.TYPE_CLASS.TYPE_BYTE : 
		memcpy(ADR(_BYTE),Source.pValue,Source.diSize);
		convertSuccessful := TryConvert_Source_TO_Destination(_BYTE,_UDINT);
	
	__SYSTEM.TYPE_CLASS.TYPE_WORD : 
		memcpy(ADR(_WORD),Source.pValue,Source.diSize);
		convertSuccessful := TryConvert_Source_TO_Destination(_WORD,_UDINT);
		
	__SYSTEM.TYPE_CLASS.TYPE_DWORD : 
		memcpy(ADR(_DWORD),Source.pValue,Source.diSize);
		convertSuccessful := TryConvert_Source_TO_Destination(_DWORD,_UDINT);
		
	__SYSTEM.TYPE_CLASS.TYPE_USINT : 
		memcpy(ADR(_USINT),Source.pValue,Source.diSize);
		convertSuccessful := TryConvert_Source_TO_Destination(_USINT,_UDINT);
		
	__SYSTEM.TYPE_CLASS.TYPE_UINT : 
		memcpy(ADR(_UINT),Source.pValue,Source.diSize);
		convertSuccessful := TryConvert_Source_TO_Destination(_UINT,_UDINT);
		
	__SYSTEM.TYPE_CLASS.TYPE_UDINT : 
		memcpy(ADR(_UDINT),Source.pValue,Source.diSize);
		convertSuccessful := TRUE;
		
ELSE 
		
	RETURN;
		
END_CASE

IF NOT convertSuccessful THEN
	RETURN;
END_IF

SetUint(jsonValue,_UDINT); // Not a typo, SetUnit actually takes a UDINT
TrySetUint := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="TrySetUint64" Id="{c9a8c18d-4cf6-42df-9701-a9243833dd8f}" FolderPath="Private\">
      <Declaration><![CDATA[METHOD PRIVATE TrySetUint64 : BOOL
VAR_INPUT
	JsonValue : SJsonValue;
	Source : ANY;
END_VAR
VAR
	convertSuccessful : BOOL;
	_LWORD : LWORD;
	_ULINT : LINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE Source.TypeClass OF 

	__SYSTEM.TYPE_CLASS.TYPE_LWORD : 
		memcpy(ADR(_LWORD),Source.pValue,Source.diSize);
		convertSuccessful := TryConvert_Source_TO_Destination(_LWORD,_ULINT);
	
	__SYSTEM.TYPE_CLASS.TYPE_ULINT : 
		memcpy(ADR(_ULINT),Source.pValue,Source.diSize);
		convertSuccessful := TRUE;
		
ELSE 
		
	RETURN;
		
END_CASE

IF NOT convertSuccessful THEN
	RETURN;
END_IF

SetUint64(jsonValue,_ULINT);
TrySetUint64 := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="TryWrite" Id="{9409e7d4-16a3-4ef6-99bb-41a32322606b}">
      <Declaration><![CDATA[METHOD PUBLIC TryWrite : BOOL
VAR_INPUT
	Path : T_MaxString;
	Source : ANY;
END_VAR
VAR
	jsonDomCreator : JsonDomCreator(THIS^);
	jsonPathSyntaxParser : JsonPathSyntaxParser;
	jsonValue : SJsonValue;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[jsonPathSyntaxParser.Parse(Path, jsonDomCreator);
jsonValue := jsonDomCreator.CurrentNode;

IF jsonValue = 0 THEN
	RETURN;
END_IF

IF IsObject(jsonValue) THEN
	RETURN;
END_IF

IF TrySetBool(jsonValue, Source) THEN
	TryWrite := TRUE;
	RETURN;
END_IF

IF TrySetString(jsonValue, Source) THEN
	TryWrite := TRUE;
	RETURN;
END_IF

IF TrySetInt(jsonValue, Source) THEN
	TryWrite := TRUE;
	RETURN;
END_IF

IF TrySetUint(jsonValue, Source) THEN
	TryWrite := TRUE;
	RETURN;
END_IF

IF TrySetDouble(jsonValue, Source) THEN
	TryWrite := TRUE;
	RETURN;
END_IF

IF TrySetInt64(jsonValue, Source) THEN
	TryWrite := TRUE;
	RETURN;
END_IF

IF TrySetUint64(jsonValue, Source) THEN
	TryWrite := TRUE;
	RETURN;
END_IF]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>